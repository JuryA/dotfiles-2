#!/usr/bin/env zsh
# This came from Greg V's dotfiles:
#      https://github.com/myfreeweb/dotfiles
# Feel free to steal it, but attribution is nice
#
# Because keychain is fscking 1500 lines long.

autoload colors && colors

COLORIZE=$(cat << _EOF_
{
	FS = " "; ORS = ""; OFS = "";
	print " $fg_bold[cyan]",\$1,"$reset_color\t$fg_no_bold[magenta]",\$2,"$reset_color ";
	\$1 = \$2 = "";
	OFS = " ";
	sub("\\\(DSA\\\)",       "[_$fg_bold[red]DSA${reset_color}_]");
	sub("\\\(RSA\\\)",       "[_$fg_bold[red]RSA${reset_color}_]");
	sub("\\\(ED25519\\\)",   "[$fg_bold[green]EdDSA$reset_color]");
	sub("\\\(ECDSA\\\)",     "[$fg_bold[yellow]ECDSA$reset_color]");
	print \$NF," ";
	\$NF = "";
	print \$0,"\n";
}
_EOF_)

keynames=(winterhold github bitbucket clojars heroku)
(( $# > 0 )) && keynames=($@)
keys=()
for k in $keynames; do
	[[ -e "$HOME/.ssh/$k" ]] && keys+="$HOME/.ssh/$k"
done

echo " *  $fg_bold[blue]load$fg_bold[green]keys$reset_color  * "

ssh-add $keys 2>/dev/null && echo " $fg_bold[green]✔ SSH$reset_color"
ssh-add -l | awk $COLORIZE

if [[ -e ~/.gnupg/enable-agent ]]; then
	GPG_KEY="0x5F9F528D3B011BAF"
	echo | gpg --no-options --use-agent --no-tty --sign --local-user $GPG_KEY -o- >/dev/null 2>&1 \
		&& echo " $fg_bold[green]✔ GPG\n\t$fg_no_bold[magenta]$GPG_KEY$reset_color" \
		|| echo " $fg_bold[red]✗ GPG\n\t$fg_no_bold[magenta]$GPG_KEY$reset_color"
fi
