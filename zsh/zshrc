# This came from Greg V's dotfiles:
#      https://github.com/myfreeweb/dotfiles
# Feel free to steal it, but attribution is nice
#
# thanks:
# http://selena.deckelmann.usesthis.com/
# https://github.com/mathiasbynens/dotfiles/blob/master/.functions
# https://superuser.com/questions/180148/how-do-you-get-screen-to-automatically-connect-to-the-current-ssh-agent-when-re
# https://github.com/nummi/my-config/blob/master/.zsh/completion.zsh
# http://robots.thoughtbot.com/silver-searcher-tab-completion-with-exuberant-ctags
# http://iterm2.com/shell_integration.html

# External plugins {{{
source ~/.zsh/zsh-hl/zsh-syntax-highlighting.zsh
source ~/.zsh/zsh-hss/zsh-history-substring-search.zsh
# }}}

# Color {{{
base16() {
	local BASE16_SCHEME="$*"
	local BASE16_SHELL="$HOME/.zsh/base16-shell/base16-$BASE16_SCHEME.dark.sh"
	if [[ -s $BASE16_SHELL ]]; then
		source $BASE16_SHELL
		export BASE16_SCHEME
		echo -n $BASE16_SCHEME > ~/.base16.scheme
	else
		echo "Looks like $BASE16_SHELL does not exist :-("
	fi
}
_base16() { compadd $(find ~/.zsh/base16-shell | grep dark.sh | xargs basename | sed -e "s/base16-//" -e "s/\.dark\.sh//") }
export BASE16_SCHEME="$(cat ~/.base16.scheme 2>/dev/null || echo default)"
base16 $BASE16_SCHEME
# }}}

# Support for tools, programming languages, etc {{{
# Less
export LESS_TERMCAP_mb=$'\E[01;31m'
export LESS_TERMCAP_me=$'\E[0m'
export LESS_TERMCAP_se=$'\E[0m'
export LESS_TERMCAP_so=$'\E[38;5;246m'
export LESS_TERMCAP_ue=$'\E[0m'
export LESS_TERMCAP_us=$'\E[04;33;146m'
export LESS="-R"
export MANWIDTH="120"

# Go
export GOPATH=~
[[ -e $commands[brew] ]] && export BREWGO=$(brew --prefix go)

# JS
[[ -d /usr/local/lib/node_modules ]] && export NODE_PATH=/usr/local/lib/node_modules

# Java
[[ -e /usr/libexec/java_home ]] && export JAVA_HOME=$(/usr/libexec/java_home)
[[ -d /usr/local/opt/android-sdk ]] && export ANDROID_HOME=/usr/local/opt/android-sdk
export JRUBY_OPTS="-J-XX:+TieredCompilation -J-XX:TieredStopAtLevel=1 -J-noverify"
runjava() {
	cp $1 /tmp/Main.java
	(cd /tmp && javac Main.java && echo "--- compiled, running" && java Main)
}

# .NET Core
[[ -e $commands[dnvm.sh] ]] && source dnvm.sh

# Ruby
export RBXOPT=-X19
[[ -d /usr/local/var/rbenv ]] && export RBENV_ROOT=/usr/local/var/rbenv
bforkify() {
	bundle config local.$1 ~/src/github.com/myfreeweb/$1
}
bunforkify() {
	bundle config --delete local.$1
}

# Python
export PYTHONDONTWRITEBYTECODE=true
export VIRTUALENV_DISTRIBUTE=true
export PIP_USE_MIRRORS=true
export PYTHONSTARTUP=~/.pythonrc
export PYTHONUSERBASE=~ # pip install --user

# Compiling stuff on OS X
[[ -e $commands[brew] ]] && export ARCHFLAGS="-arch x86_64"

# OCaml
[[ -e $commands[opam] ]] && eval $(opam config env)

# Haskell
:test() { make repl } # for the vimux/ghci trick
# }}}

# Aliases and functions {{{
[[ -e ~/.rc ]] && source ~/.rc
[[ ! -e $commands[gmake] ]] && alias gmake='make'
dighost() { host $(dig $1 | grep ANSWER -C 1 | tail -n 1 | awk '{ print $5 }') }
mcd() { mkdir -p "$1" && cd "$1"; }
icd() { DIR=$(ls | peco) && cd $DIR && icd }
prj() { cd ~/src/$(ghq list | peco) }
hst() { eval $(history -n 1 | tail -r | peco --layout=bottom-up) }
fuck() { sudo $(history -n -1) }

if [[ -n $TMUX ]]; then
	escseq() { printf "\033Ptmux;\033\033]$1\007\033\\" }
else
	escseq() { printf "\033]$1\007" }
fi

if [[ -n $ITERM_PROFILE && -o login ]]; then
	before_cmd_executes() { escseq "133;C" }
	after_cmd_executes() {
		escseq "133;D;$1"
		escseq "1337;RemoteHost=$USER@$(hostname -f)"
		escseq "1337;CurrentDir=$PWD"
	}
	escseq "1337;ShellIntegrationVersion=1"
	after_cmd_executes 0
else
	before_cmd_executes() { }
	after_cmd_executes() { }
fi

preexec() {
	before_cmd_executes
}

precmd() {
	# $? in prompt is wrong, can't pass %? to conditionals
	local exit_status="$?"
	if [[ $exit_status == 0 ]]; then SMILEY=')'; else SMILEY='('; fi
	after_cmd_executes $exit_status
	if [[ $PRIVATE == 1 ]]; then
		SCOLOR=$bg[magenta]$fg_bold[black]
	elif [[ ${HOST##*\.} == 'local' || ${HOST##*\.} == 'localdomain' || $HOST =~ '^[^\.]+$' ]]; then
		# green on *.local, *.localdomain and hostnames without dots
		SCOLOR=$fg_bold[green]
	else
		# red everything else - that is, on production servers!
		SCOLOR=$fg_bold[red]
	fi
}
# }}}

# Basic settings {{{
# Modules
autoload -U colors && colors
autoload -U compinit && zmodload -i zsh/complist
autoload -U edit-command-line && zle -N edit-command-line
autoload -U url-quote-magic && zle -N self-insert url-quote-magic
autoload -U select-word-style && select-word-style bash
autoload -U zmv
zle -N hst
# Keybindings
bindkey -e # Emacs style keys in shell
bindkey '^xe' edit-command-line
bindkey '^x^e' edit-command-line
bindkey "\e[3~" delete-char # Del
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
bindkey '^R' hst
# Stuff
setopt pushd_ignore_dups; setopt auto_pushd
setopt auto_name_dirs; setopt auto_cd
setopt transient_rprompt; setopt prompt_subst
setopt no_beep; setopt multios
setopt extended_glob; setopt interactive_comments
# History
setopt hist_ignore_dups; setopt hist_ignore_space
setopt hist_reduce_blanks; setopt hist_verify
setopt hist_expire_dups_first; setopt hist_find_no_dups
setopt share_history; setopt extended_history
setopt append_history; setopt inc_append_history
# Completion
setopt complete_in_word
setopt no_complete_aliases # Actually: completes aliases! (I guess that means "no ~separate functions~ for aliases")
unsetopt always_to_end
zstyle ':completion:*' squeeze-slashes true
zstyle ':completion:*' insert-tab pending
zstyle ':completion:*' expand "yes"
zstyle ':completion:*' matcher-list "m:{a-zA-Z}={A-Za-z}" # ignore case
zstyle ':completion:*' list-colors ""
zstyle ':completion:*' menu select=2 _complete _ignored _approximate
zstyle ':completion:*' group-name ''
zstyle ':completion:*' verbose yes
zstyle ':completion:*:descriptions' format "- %{${fg[yellow]}%}%d%{${reset_color}%} -"
zstyle ':completion:*:messages' format $'\e[00;31m%d'
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:manuals.(^1*)' insert-sections true
zstyle ':completion:*::::' completer _expand _complete _ignored _approximate
zstyle ':completion:*:match:*' original only
zstyle ':completion:*:approximate:*' max-errors 1 numeric
zstyle ':completion:*:cd:*' ignore-parents parent pwd
zstyle ':completion:*:rm:*' ignore-line yes
zstyle ':completion:*:*:*:processes' list-colors "=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01"
zstyle ':completion:*:*:*:processes' command "ps -u $(whoami) -o pid,user,comm -w -w"
zstyle ':completion:*:*:*:*:hosts' list-colors "=*=$color[cyan];$color[bg-black]"
zstyle ':completion:*:functions' ignored-patterns "_*"
zstyle ':completion:*:original' list-colors "=*=$color[red];$color[bold]"
zstyle ':completion:*:parameters' list-colors "=[^a-zA-Z]*=$color[red]"
zstyle ':completion:*:aliases' list-colors "=*=$color[green]"
fpath=(~/.zsh/zsh-completions/src(N) /usr/local/share/zsh/site-functions(N) $fpath)
fignore+=.DS_Store
compinit -i
compdef mcd=cd
_ctagscomp() { (( CURRENT == 2 )) && compadd $(cut -f 1 tags ctags .git/tags tmp/tags 2>/dev/null | grep -v '!_TAG') }
compdef _ctagscomp ack
compdef _ctagscomp ag
compdef _ctagscomp pt
_loadkeys() { compadd $(find ~/.ssh -perm 0600 ! -type s | xargs basename) }
compdef _loadkeys loadkeys
compdef _base16 base16

HISTFILE=~/.zsh_history
HISTSIZE=4096
SAVEHIST=4096
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
WORDCHARS='*?_-.[]~=&;!#$%^(){}<>' # Like default, but without / -- ^W must be useful in paths, like it is in vim, bash, tcsh
export LANG="en_US.UTF-8"
export LC_ALL="en_US.UTF-8"
export CLICOLOR="yes"
export PAGER="less"
export EDITOR="vim"
[[ -e $commands[nvim] ]] && export EDITOR="nvim" && alias vi="nvim"

PROMPT='%{$SCOLOR%}${PWD/#~/~}%{$reset_color%} %{$fg_no_bold[yellow]%}:$SMILEY%{$reset_color%} '

cdpath=(~/src/github.com/myfreeweb ~/src/bitbucket.org/wmru)

typeset -U path # U for Unique, like a set; (N) == only if exists
path=(
	.git/safe/../../bin
	~/bin(N) ~/.cask/bin(N)
	$BREWGO/bin(N) $BREWGO/libexec/bin(N) /usr/local/go/bin(N)
	$JAVA_HOME/bin(N)
	$RBENV_ROOT/bin(N)
	/usr/local/share/npm/bin(N)
	/usr/local/sbin /usr/local/bin /sbin /bin $path
)
export PATH
# }}}

# Arcane SSH/GPG agent stuff {{{
SSH_SOCK_LN=~/.ssh/ssh_auth_sock
if [[ $SSH_AUTH_SOCK =~ '^/private/tmp/com.apple.*' || ! -e $SSH_AUTH_SOCK ]]; then
	# Apple-provided or none => run agent right at $SSH_SOCK_LN
	[[ ! -e $SSH_SOCK_LN && -e $commands[ssh-agent] ]] && eval $(ssh-agent -a $SSH_SOCK_LN)
elif [[ -S $SSH_AUTH_SOCK && ! -h $SSH_AUTH_SOCK && $SSH_AUTH_SOCK != $SSH_SOCK_LN ]]; then
	# Socket and not symlink (== forwarded!) => make the symlink
	ln -sf $SSH_AUTH_SOCK $SSH_SOCK_LN
fi
export SSH_AUTH_SOCK=$SSH_SOCK_LN

if [[ -e ~/.gnupg/enable-agent ]]; then
	[[ -e ~/.gnupg/agent-info ]] && source ~/.gnupg/agent-info
	[[ ! -S $(echo $GPG_AGENT_INFO | cut -d : -f 1) ]] && eval $(gpg-agent --daemon --write-env-file ~/.gnupg/agent-info)
	export GPG_AGENT_INFO # not exported in the env-file
fi
export GPG_TTY=$(tty)
# }}}

[[ -e $commands[showkeys] ]] && showkeys
[[ -e $commands[fortune] ]] && fortune | (cowsay -f small || cat) 2&> /dev/null | (lolcat || cat) 2&>/dev/null
[[ -e ~/.zshrc.local ]] && source ~/.zshrc.local
[[ $PRIVATE == 1 ]] && unset HISTFILE
